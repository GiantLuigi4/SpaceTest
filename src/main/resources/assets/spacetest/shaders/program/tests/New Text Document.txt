// Written by GLtracy

// math const
const float PI = 3.14159265359;
const float MAX = 10000.0;

// scatter const
const float R_INNER = 1.0;
const float R = R_INNER + 0.5;

const int NUM_OUT_SCATTER = 8;
const int NUM_IN_SCATTER = 80;

vec2 intersectSphere(vec3 rayOrigin, vec3 rayDir, float radius)
{
	float b = dot( rayOrigin, rayDir );
	float c = dot( rayOrigin, rayOrigin ) - radius * radius;
	
	float d = b * b - c;
	if ( d < 0.0 )
		return vec2( MAX, -MAX );
	d = sqrt(d);
    
    float near = -b - d;
    float far = -b + d;
	
	return vec2(near, far);
}

// Mie
// g : ( -0.75, -0.999 )
//      3 * ( 1 - g^2 )               1 + c^2
// F = ----------------- * -------------------------------
//      8pi * ( 2 + g^2 )     ( 1 + g^2 - 2 * g * c )^(3/2)
float miePhase(float g, float c, float cc)
{
	float gg = g * g;
	
	float a = (1.0 - gg) * (1.0 + cc);

	float b = 1.0 + gg - 2.0 * g * c;
	b *= sqrt(b);
	b *= 2.0 + gg;	
	
	return (3.0 / 8.0 / PI) * a / b;
}

// Rayleigh
// g : 0
// F = 3/16PI * ( 1 + c^2 )
float rayleighPhase(float cc)
{
	return (3.0 / 16.0 / PI) * (1.0 + cc);
}

float density(vec3 pos, float atmosphereRadius, float ph)
{
	return exp(-max( length(pos) - atmosphereRadius, 0.0) / ph);
}

float optic(vec3 pos, vec3 q, float atmosphereRadius, float ph)
{
	vec3 s = (q - pos) / float( NUM_OUT_SCATTER );
	vec3 v = pos + s * 0.5;
	
	float sum = 0.0;
	for (int i = 0; i < NUM_OUT_SCATTER; ++i)
    {
		sum += density(v, atmosphereRadius, ph);
		v += s;
	}
	sum *= length(s);
	
	return sum;
}

vec3 inScatter(vec3 rayOrigin, vec3 rayDir, float planetRadius, float atmosphereRadius, vec2 hit, vec3 sunDir)
{
	const float ph_ray = 0.05;
    const float ph_mie = 0.02;
    
    const vec3 k_ray = vec3(3.8, 13.5, 33.1);
    const vec3 k_mie = vec3(21.0);
    const float k_mie_ex = 1.1;
    
	vec3 sum_ray = vec3(0.0);
    vec3 sum_mie = vec3(0.0);
    
    float n_ray0 = 0.0;
    float n_mie0 = 0.0;
    
	float len = ( hit.y - hit.x ) / float( NUM_IN_SCATTER );
    vec3 s = rayDir * len;
	vec3 v = rayOrigin + rayDir * ( hit.x + len * 0.5 );
    
    for ( int i = 0; i < NUM_IN_SCATTER; ++i, v += s )
    {   
		float d_ray = density( v, planetRadius, ph_ray ) * len;
        float d_mie = density( v, planetRadius, ph_mie ) * len;
        
        n_ray0 += d_ray;
        n_mie0 += d_mie;
        
        vec2 f = intersectSphere( v, sunDir, atmosphereRadius );
		vec3 u = v + sunDir * f.y;
        
        float n_ray1 = optic( v, u, planetRadius, ph_ray );
        float n_mie1 = optic( v, u, planetRadius, ph_mie );
		
        vec3 att = exp( - ( n_ray0 + n_ray1 ) * k_ray - ( n_mie0 + n_mie1 ) * k_mie * k_mie_ex );
        
		sum_ray += d_ray * att;
        sum_mie += d_mie * att;
	}
	
	float c  = dot(rayDir, -sunDir);
	float cc = c * c;
    vec3 scatter =
        sum_ray * k_ray * rayleighPhase( cc ) +
     	sum_mie * k_mie * miePhase( -0.78, c, cc );
    
	
	return 10.0 * scatter;
}

// angle : pitch, yaw
mat3 rot3xy( vec2 angle ) {
	vec2 c = cos( angle );
	vec2 s = sin( angle );
	
	return mat3(
		c.y      ,  0.0, -s.y,
		s.y * s.x,  c.x,  c.y * s.x,
		s.y * c.x, -s.x,  c.y * c.x
	);
}

// ray direction
vec3 ray_dir( float fov, vec2 size, vec2 pos ) {
	vec2 xy = pos - size * 0.5;

	float cot_half_fov = tan( radians( 90.0 - fov * 0.5 ) );	
	float z = size.y * 0.5 * cot_half_fov;
	
	return normalize( vec3( xy, -z ) );
}

void mainImage(out vec4 fragColor, in vec2 fragCoord)
{
    vec3 center = vec3(0.0, 0.0, 0.0);
 
	// default ray dir
	vec3 dir = ray_dir( 45.0, iResolution.xy, fragCoord.xy );
	
	// default ray origin
	vec3 eye = vec3( 0.0, 0.0, 3.0 ) - center;

	// rotate camera
	mat3 rot = rot3xy( vec2( 0.0, iTime * 0.5 ) );
	dir = rot * dir;
	eye = rot * eye;
	
	// sun light dir
	vec3 l = vec3( 0.0, 0.0, 1.0 );
			  
	vec2 e = intersectSphere( eye, dir, R );
	if (e.x > e.y) {
		fragColor = vec4( 0.0, 0.0, 0.0, 1.0 );
        return;
	}
	
	vec2 f = intersectSphere( eye, dir, R_INNER );
	e.y = min( e.y, f.x );

	vec3 I = inScatter( eye, dir, R_INNER, R, e, l );
	
	fragColor = vec4( pow( I, vec3( 1.0 / 2.2 ) ), 1.0 );
}